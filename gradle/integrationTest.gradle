apply from: "$rootDir/gradle/databaseSetup.gradle"

sourceSets {
    integrationTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.junitXml.destination = file("$reports.junitXml.destination/integration")
    dependsOn startAndPrepareDatabase
    finalizedBy stopDatabase
}

check.dependsOn integrationTest

integrationTest.jacoco.destinationFile = file("$jacocoReportsExecDir/integration.exec")

task jacocoIntegrationTestReport(type: JacocoReport) {
    executionData integrationTest
    sourceSets sourceSets.main

    reports {
        html.destination file("$jacocoReportsHtmlDir/integration")
    }
}

sonarRunner {
    sonarProperties {
        properties['sonar.tests'] += sourceSets.integrationTest.allSource.srcDirs
    }
}